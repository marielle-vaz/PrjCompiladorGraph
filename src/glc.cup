import java_cup.runtime.*;
import classes_graph.Graph;

parser code {:
    public Graph grafo;
    public boolean deveImprimir = false;
    private boolean tipoDefinido = false;

    public void syntax_error(Symbol s){
        this.defineError(s.left, s.right);
    }

    public void defineError(int line, int column, String text) {
        Yylex scanner = (Yylex) this.getScanner();
        scanner.defineError(line, column, text);
    }

    public void defineError(int linha, int coluna){
        Yylex scanner = (Yylex) this.getScanner();
        scanner.defineError(linha, coluna);
    }

    public void defineError(String text) {
        Yylex scanner = (Yylex) this.getScanner();
        scanner.defineError(text);
    }
:};

terminal GRAPH, colon, line, arrow, directed, undirected, print, vertex, edge;
terminal String ID, adjacency;

non terminal Inicio, TipoGrafo, ListaElementos, Elemento, Impressao;

start with Inicio;

Inicio ::= GRAPH colon TipoGrafo ListaElementos Impressao 
| error 
    {: 
        parser.defineError("Comando inicial inválido. Esperado: GRAPH:");
    :}
  ;

TipoGrafo ::= directed 
    {: 
        grafo = new Graph(true); 
        tipoDefinido = true;
    :}
  | undirected 
    {: 
        grafo = new Graph(false); 
        tipoDefinido = true;
    :}
  | error 
    {: 
        parser.defineError("Sintaxe inválida! O tipo deve ser directed ou undirected.");
    :}
  ;

ListaElementos ::= ListaElementos Elemento
                 | Elemento ;

Elemento ::= vertex ID:id 
    {: 
        if (!tipoDefinido || grafo == null) {
            parser.defineError("Tipo de grafo não definido antes da criação de arestas.");
        } else if (grafo.hasVertex(id)) {
            parser.defineError(idleft, idright, "Vértice duplicado: " + id);
        } else {
            grafo.addVertex(id);
        }
    :}
  | edge ID:origem arrow ID:destino 
    {: 
        if (!tipoDefinido || grafo == null) {
            parser.defineError("Tipo de grafo não definido antes da criação de arestas.");
        } else if (!grafo.hasVertex(origem) || !grafo.hasVertex(destino)) {
            parser.defineError(origemleft, destinoright, "Vértice inexistente usado na aresta: " + origem + " -> " + destino);
        } else if (grafo.hasEdge(origem, destino)) {
            parser.defineError(origemleft, destinoright, "Aresta duplicada: " + origem + " -> " + destino);
        } else {
            grafo.addEdge(origem, destino);
        }
    :}
  | edge ID:origem line ID:destino 
    {: 
        if (!tipoDefinido || grafo == null) {
            parser.defineError("Tipo de grafo não definido antes da criação de arestas.");
        } else if (!grafo.hasVertex(origem) || !grafo.hasVertex(destino)) {
            parser.defineError(origemleft, destinoright, "Vértice inexistente usado na aresta: " + origem + " -- " + destino);
        } else if (grafo.hasEdge(origem, destino)) {
            parser.defineError(origemleft, destinoright, "Aresta duplicada: " + origem + " -- " + destino);
        } else {
            grafo.addEdge(origem, destino);
        }
    :}
  | error 
    {: 
        parser.defineError("Elemento mal formado na definição de vértices/arestas.");
    :}
  ;

Impressao ::= print adjacency 
    {: 
        deveImprimir = true; 
    :}
  | error 
    {: 
        parser.defineError("Elemento mal formado na chamada do 'print adjacency'");
    :}
  ;
